import shadow:utility;
import shadow:natives;
import shadow:io@Console;

/**
 * Class {@code Thread} provides ways to manipulate threads. 
 * All threads are required to terminate for the main process to terminate as well.
 * If the main method returns and there are still running threads ... what will happen?
 *
 * @author Claude Abounegm
 * @author Barry Wittman
 */
locked class shadow:standard@
	Thread
is CanIndex<int, Thread>
{
	public constant int READY = 0;
	public constant int RUNNING = 1;
	public constant int WAITING = 2;
	public constant int DEAD = 3;
	
	[ImportAssembly]
	private readonly __shadow_standard__Thread_spawn() => (Pointer);

	[ImportNative]
	private get staticNextId() => (int);

	// START: Private variables
	nullable Exception ex = null;
	//nullable SignalToken interruptToken = null;
	ConditionVariable conditionVariable; // used for all thread-safety, sleeping, and joining
	nullable ConditionVariable waitingVariable; // the condition variable a thread is waiting on (perhaps from another thread)
	
	int state;
	boolean interrupting;
	boolean joined;
	CanRun runner;
	
	ThreadSafeList<Thread> childrenList;

	//Mutex mutex;
	//Mutex stateMutex;
	
	//Signaler signaler;
	Mailbox mailbox;
	// END: Private variables

	/// Gets the Shadow-specific thread ID.
	get int id;
	
	/// Gets the main thread, which is the root of all threads.
	[ImportNative]
	public readonly get main() => (Thread);
	
	/**
	* Gets the {@code Thread} which spawned this thread. The Main thread does not
	* have a parent, and is null.
	*/
	get nullable Thread parent = null;
	
	/**
	* Gets the name of the thread set at spawn time. If no name is set
	* at spawn time, the name takes the format "Thread#{id}".
	*/
	get String name;
	
	/**
	* Gets whether this thread is running.
	*
	* @return {@code true} if the thread is alive; otherwise, {@code false}.
	*/
	public readonly get isAlive() => (boolean)
	{
	    return this->state != DEAD;
	}
	
	private set state(int state) => ()
	{
	    conditionVariable.lock();
		this:state = state;
		conditionVariable.unlock();
	}
	
	/// Gets the state of this thread. It can be one of {@code READY}, {@code RUNNING}, {@code WAITING}, or {@code DEAD}.
	public readonly get state() => (int)
	{
        conditionVariable.lock();
        int result = state;
        conditionVariable.unlock();
        return result;
	}
	
	/// Gets the string representation of the state of this thread.
	public readonly get stateString() => (String)
	{
		switch(this->state) {
			case (READY)
				return "READY";
			
			case (RUNNING)
				return "RUNNING";
				
			case (WAITING)
				return "WAITING";
				
			case (DEAD)
				return "DEAD";
		}
		
		throw InvalidOperationException:create("Invalid state");
	}
	
	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread.
	* 
	* @param runner The instance that contains a run function.
	*/
	protected create(CanRun runner)
	{
		this(null, runner);
	}

	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be run
	* on a different thread, and takes a {@code String} which is the name
	* of the thread for reference purposes.
	* 
	* @param name The name of the thread.
	* @param runner The instance that contains a run function.
	*/
	protected create(nullable String name, CanRun runner)
	{
		this:id = this->staticNextId;

		this:childrenList = ThreadSafeList<Thread>:create();
		
		try {
			this:name = check(name);
		} recover {
			this:name = "Thread#" # id;
		}

        this:conditionVariable = ConditionVariable:create();

		// initialize the mailbox with an initial capacity of 8, and a max capacity of 32.
		// the mailbox doubles as more items are sent to it, until it reaches 32, and blocks
		// if more items are added, until space becomes available again by receiving messages.
		this:mailbox = Mailbox:create(/*instance(),*/ 8, 32);
		
		this:state = READY;

		if(this:id == 0) {
			this:runner = runner;
			this:state = RUNNING;
		} else {
			// better way to clone the runner with TAC? We need to clone even the parent
			// so there would be no variable sharing at all.
			// Edit: No, I think Thread contents are the one things that *should* be shared in Shadow.
			// Otherwise, how can the parent Thread have control over its children?
			this:runner = copy(runner);
			
			this:parent = currentThread();
			Pointer handle = __shadow_standard__Thread_spawn();

            if(!handle->isValid) {
                throw ThreadSpawnException:create();
            }

            // This is done awkwardly in TAC generation but could come back here if leaking were allowed in creates
            // Legal leaking could only be A) after all initialization is done and B) inside of locked methods, since
            // non-locked methods could leak before subsequent child creates were called
			// check(parent):childrenList.add(this);

			// sleep(1); // necessary?
		}
	}

	protected addChild(Thread thread) => ()
	{
        childrenList.add(thread);
	}
	
	/**
	* {@code Interruptible}. Suspends the execution of the calling thread until this thread terminates.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	* 
	* @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	* @throws InterruptedException if another thread requested to interrupt this thread.
	*
	* @seeDoc shadow:standard@ThreadException
	*/
	public join() => ()
	{
		join(Time->InfiniteTimeout);
	}

	/**
	* {@code Interruptible}. Suspends the execution of the calling thread until this thread terminates or until it times out.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	*
	* @param millisecondsTimeout the time to wait for in milliseconds.
	* @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
	*
	* @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	* @throws InterruptedException if another thread requested to interrupt this thread.
	*
	* @seeDoc shadow:standard@ThreadException
	*/
	public join(int millisecondsTimeout) => (boolean)
	{
		return join(Time.fromMilliSeconds(millisecondsTimeout));
	}

	/**
    * {@code Interruptible}. Suspends the execution of the calling thread until this thread terminates or until it times out.
    * Joining on a dead thread has no side effects. If the thread is terminated because of an exception,
    * the exception is thrown when this method is called.
    *
    * @param timeout the time to wait for.
    * @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
    *
    * @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
    * @throws ThreadException if the thread terminated due to an exception.
    * @throws InterruptedException if another thread requested to interrupt this thread.
    *
    * @seeDoc shadow:standard@ThreadException
    */
	public join(TimeSpan timeout) => (boolean)
	{
	    Thread currentThread = CurrentThread->instance;

		if(this === main()) {
			throw InvalidOperationException:create("A thread cannot call join on the main thread.");
		}
		
		if(this === currentThread) {
			throw InvalidOperationException:create("A thread cannot call join on itself.");
		}

		conditionVariable.lock();
		boolean throwException = !joined;
        long currentTime = System.epochNanoTime();
        long finishedTime = currentTime + timeout.totalNanoSeconds();
		boolean waiting = !timeout->isPositive or currentTime < finishedTime; // ignore time if not positive

        while (isAlive() and waiting) {
            currentThread:conditionVariable.lock();
            currentThread:state = WAITING;
            currentThread:waitingVariable = conditionVariable;

            if (currentThread:interrupting) {
                currentThread:interrupting = false;
                currentThread:waitingVariable = null;
                currentThread:state = RUNNING;
                currentThread:conditionVariable.unlock();
                conditionVariable.unlock();
                throw InterruptedException:create();
            }
            else
                currentThread:conditionVariable.unlock();

            if (timeout->isPositive) {
                conditionVariable.wait(TimeSpan:create(finishedTime - currentTime));
                currentTime = System.epochNanoTime();
                waiting = currentTime < finishedTime;
            }
            else
                conditionVariable.wait();
        }



        joined = true;

        // this conditionVariable is NULL!!!
        currentThread:conditionVariable.lock();
        currentThread:state = RUNNING;
        currentThread:conditionVariable.unlock();

		// we then check here if we have an exception. If we do, we throw it, and
		// it is the job of runnerNative() to take care of the handling.
		if(ex !== null and throwException) {
		    conditionVariable.unlock();
			throw check(ex);
		}

		conditionVariable.unlock();

		return !waiting;
	}
	
	/**
	* Requests this thread to be interrupted. If the thread is currently in the {@code WAITING} state, 
	* the thread is interrupted and the method that caused the thread to be in the {@code WAITING} state
	* raises an {@code InterruptedException}. The methods which are interruptible are {@code join()},
	* {@code sleep()}, {@code send()}, {@code receive()}, and all their overloads.
	* If the thread is not in the {@code WAITING} state, an interrupt flag is set, and the thread is interrupted
	* as soon as one of the above interruptible methods above are called, or when the user calls 
	* {@code CurrentThread.throwIfInterrupting()}, or throws an exception using {@code CurrentThread->interrupting}.
	*
	* @return {@code true} if the thread was marked as interrupted or {@code false} if the thread wasn't waiting.
	*/
	public interrupt() => ()
	{
		if(!isAlive())
			return;
		
		conditionVariable.lock();

        // Whether or not we're currently waiting, we might be soon, and we want to interrupt immediately
        interrupting = true;

        if(this->state == WAITING) {
        	conditionVariable.unlock();
            // Should never be null
            try {
                check(waitingVariable).notifyAll();
            }
            recover {}
        }
        else
            conditionVariable.unlock();

        // Quick explanation on how exceptions propagate:
        //
        // When the InterruptedException is raised, the runnerNative() catches that exception.
        // Just like any other exception, `ex` is set to that exception, and runnerNative() then cleans
        // up the resources it used. Since all threads get woken up after
        // the exception `ex` has been set, all the joined thread also get an InterruptedException correctly.
    }

	/**
	* {@code Interruptible}
	* @throws InterruptedException
	* @seeDoc shadow:natives@Signaler
	*/
	[ExportMethod(CurrentThread:class)]
	private sleep(TimeSpan timeout) => ()
	{
	    // Should only be called on current thread
        conditionVariable.lock();
        long currentTime = System.epochNanoTime();
        long finishedTime = currentTime + timeout.totalNanoSeconds();
        boolean sleeping = currentTime < finishedTime;
        while (isAlive() and sleeping) {
            state = WAITING;
            waitingVariable = conditionVariable; // sleeping on its own condition variable

            if (interrupting) {
                interrupting = false;
                waitingVariable = null;
                state = RUNNING;
                conditionVariable.unlock();
                throw InterruptedException:create();
            }

            conditionVariable.wait(TimeSpan:create(finishedTime - currentTime));
            currentTime = System.epochNanoTime();
            sleeping = currentTime < finishedTime;
        }

        state = RUNNING;

        conditionVariable.unlock();
	}
	
	/**
	* {@code Interruptible}
	* @throws InterruptedException
	* @seeDoc shadow:natives@Signaler
	*/
	[ExportMethod(CurrentThread:class)]
	private sleep(int millisecondsTimeout) => ()
	{
		sleep(Time.fromMilliSeconds(millisecondsTimeout));
	}

	/**
	* {@code Interruptible}. Receives data with the desired {@code expectedType}. This method will throw an exception 
	* and will not retrieve the message if the expected type is not the same as the actual one
	* sent by the thread. A thread can send as many messages as it desires, but the receiving thread
	* should always receive the data in the order it was sent, or otherwise an exception is thrown.
	*
	* @throws IncompatibleMessageTypeException If the item attempted to retrieve does not have the same type as expectedType.
	* @throws InterruptedException If this thread was interrupted while waiting on an empty mailbox.
	* @throws IllegalArgumentException If this thread is the same as the from thread.
	* 
	* @seeDoc shadow:standard@Mailbox
	*/
	[ExportMethod(Object:class)]
	protected receiveFirst(Class expectedType) => (Object, Thread)
	{
		return mailbox.receiveFirst(expectedType);
	}
	
	/**
	* {@code Interruptible}. Receives data with the desired {@code expectedType} from the desired thread.
	* This method will throw an exception and will not retrieve the message if the
	* expected type is not the same as the actual one sent by the thread. A thread
	* can send as many messages as it desires, but the receiving thread should always
	* receive the data in the order it was sent, or otherwise an exception is thrown.
	*
	* @throws IncompatibleMessageTypeException If the item attempted to retrieve does not have the same type as expectedType.
	* @throws InterruptedException If this thread was interrupted while waiting on an empty mailbox.
	* @throws IllegalArgumentException If this thread is the same as the from thread.
	* 
	* @seeDoc shadow:standard@Mailbox
	*/
	[ExportMethod(Object:class)]
	protected receiveFirstFrom(Class expectedType, Thread from) => (Object)
	{
		if (from === this) {
			throw IllegalArgumentException:create("A Thread cannot receive a message from itself.");
		}
		
		return mailbox.receiveFirstFrom(expectedType, from);
	}
	
	/**
	* {@code Interruptible}. Sends the desired data to this thread. This method copies all the data before
	* sending it to this thread. Thus no data is shared between two threads.
	* This is a blocking method, and it will block if the mailbox of this thread is full.
	* The method will immediately unblock when there is space for the item to be
	* deposited in the mailbox. Mailbox is first come first serve, and the threads sending data
	* will unblock in the order they were blocked in.
	*
	* @param data The data to be cloned and sent to this thread.
	* 
	* @throws InterruptedException If this thread was interrupted while waiting on a full mailbox.
	* @throws InvalidOperationException if this thread is dead or is the same as the receiving one.
	*
	* @seeDoc shadow:standard@Mailbox
	*/
	protected sendTo(Object data) => ()
	{
		if (!isAlive()) {
			throw InvalidOperationException:create("Cannot send a message to a dead thread.");
		}
		
		if(isCurrentThread()) {
			throw InvalidOperationException:create("A Thread cannot send a message to itself.");
		}
		
		mailbox.sendTo(data);
	}
	
	/**
	* This method is natively executed from the newly spawned thread.
	* !!DO NOT MANUALLY EXECUTE!!
	*/
	private start() => ()
	{
		try {
		    conditionVariable.lock();
			state = RUNNING;
			conditionVariable.unlock();
			runner.run();
		} catch(Exception e) {
			ex = ThreadException:create(this, e);
		} finally {
		    conditionVariable.lock();
			state = DEAD;
			parent = null; // to avoid circular reference issues
			conditionVariable.unlock();
            conditionVariable.notifyAll();
		}
	}
	
	/// Gets whether this thread is the current executing thread.
	public readonly get isCurrentThread() => (boolean)
	{
		return (currentThread() === this);
	}
	
	/**
	* Gets the number of children the thread currently has. This method should be used with caution
	* even though it is thread safe. Since a thread can spawn threads at any time, this count can
	* drastically change between each call.
	*
	* @return how many children that thread has
	*/
	public readonly get childrenCount() => (int)
	{
		return childrenList->size;
	}
	
	/**
	* Gets the n-th child that this thread spawned. This is NOT the Shadow-specific id.
	*
	* @param index the index of the child in the range [0, childrenCount).
	* @return the n-th child {@code Thread}.
	*/
	public readonly childAt(int index) => (Thread)
	{
		return childrenList[index];
	}
	
	/**
	* Gets the n-th child that this thread spawned. This is NOT the Shadow-specific id.
	* This is a shortcut to Thread.childAt(), to allow easy indexing of children using Thread[index].
	*
	* @param index the index of the child in the range [0, childrenCount).
	* @return the n-th child {@code Thread}.
	*/
	public readonly index(int index) => (Thread)
	{
		return childAt(index);
	}
	
	/**
	* Gets the children of this thread. If {@code recurse} is true, this method
	* will also traverse the children of the children recursively, until all children
	* have been visited.
	* Care should be taken using this function, as enumerating can throw an exception
	* if the children have changed.
	*
	* @return {@code CanIterate<Thread>} An iterable class to be used with a foreach loop.
	*/
	public readonly children(boolean recurse) => (CanIterate<Thread>)
	{
	    return TraverseChildren:create(this, recurse);
	}
	
	/**
	* Gets the children of this thread. This method only enumerates through the first generation
	* children of this thread. i.e. only the thread that this thread spawned.
	*
	* @return {@code CanIterate<Thread>} An iterable class to be used with a foreach loop.
	*/
	public readonly children() => (CanIterate<Thread>)
	{
	    return TraverseChildren:create(this, false);
	}
	
	/**
	* Returns a string with the format: "Thread ({Thread->name}): {Thread->stateString}".
	*/
	public readonly toString() => (String)
	{
		return "Thread (" # name # "): " # this->stateString;
	}
	
	private readonly currentThread() => (Thread)
	{
		return CurrentThread->instance;
	}
	
	private waitForThreads() => ()
	{
		if(this !== main()) {
			throw Exception:create("waitForThreads() can only be called on the main thread.");
		}
		// this->state = DEAD; // necessary, maybe?
		
		var exceptionQueue = ArrayDeque<ThreadException>:create();

		foreach(var thread in children(true)) {
			try {
			    thread.interrupt();
				thread.join(Time->InfiniteTimeout);
				// will only throw an exception if the target
				// thread had not been joined on before.
			} catch (ThreadException e) {
				exceptionQueue.addLast(e);
			} catch (Exception e) {
			    Console.printErrorLine(e);
			}
		}

		// traverse the exceptions, if any and print them as Uncaught Thread Exceptions.
		while(!exceptionQueue.isEmpty()) {
			Console.printErrorLine("Uncaught " # exceptionQueue.removeFirst());
		}
	}
	
	private readonly createMainThread() => (Thread)
	{
		return Thread:create("Thread#main", NopRunner:create()); 
	}
	
	// A Runner which performs no operations. A null runner would had been preferred
	// but Shadow does not currently support nullable interfaces, so until then, this is
	// the most elegant solution.
	private class NopRunner
		is CanRun
	{
		public run() => () {}
	}
	
	private class TraverseChildren is CanIterate<Thread>
	{
		boolean recurse;
		Thread current;
		
		public create(Thread current, boolean recurse)
		{
		    this:current = current;
			this:recurse = recurse;
		}
		
		public readonly iterator() => (Iterator<Thread>)
		{
			if(recurse) {
				return RecursiveChildrenIterator:create(current);
			}

			return current:childrenList.iterator();
		}
		
		private class RecursiveChildrenIterator is Iterator<Thread>
		{
			ArrayDeque<Thread> threadsQueue;
			Thread current;
			
			public create(Thread current)
			{
				threadsQueue = ArrayDeque<Thread>:create();
				this:current = current;
			}
			
			public readonly hasNext() => (boolean)
			{
				return (!threadsQueue.isEmpty() or current->childrenCount > 0);
			}
			
			public next() => (Thread)
			{
				// this is tricky as we need to avoid race conditions, so we add the children
				// at the time we retrieve the next thread instead of adding them in the hasNext()
				addChildrenToQueue(current);
				
				if(threadsQueue.isEmpty()) {
					throw NoSuchElementException:create();
				}
				
				current = threadsQueue.removeFirst();
				return current;
			}
			
			private addChildrenToQueue(Thread parent) => ()
			{
				foreach(var child in parent.children()) {
					threadsQueue.addLast(child);
				}
			}
		}
	}
}