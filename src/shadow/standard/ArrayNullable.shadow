/**
 * Class {@code ArrayNullable<T>} is used to house the methods for nullable
 * array objects.
 *
 * The use of {@code NullableArray<T>} is handled by the compiler.
 * Users should avoid using the {@code NullableArray<T>} type explicitly.
 * 
 * In this implementation, most methods of {@code NullableArray<T>} are implemented
 * as aliases to the parallel method in {@code Array<T>}, since the mechanics
 * differ in only a few cases. 
 */

abstract locked class shadow:standard@
	ArrayNullable<T>
is  CanIndexNullable<long,T>
and CanIndexStoreNullable<long,T>
and CanIterateNullable<T>
{	
	get long sizeLong;
	
	/**
	 * Creates an {@code ArrayNullable<T>} wrapper for the given array.	 
	 * This method is called internally by the compiler and is only {@code protected}
	 * because it allows other classes to see the method.
	 * @unused
	 */
	[ImportNative]
	private create();
		
	/**
	 * Creates an {@code Array<T>} object with the given size.
	 * This method is called internally by the compiler and is only {@code protected}
	 * because it allows other classes to see the method.
	 */
	//[ImportNative]
	//protected create( long size );
	
	[ImportNative]
	public destroy;
	
	[ImportNative]
	public isNullable() => (boolean);
	
	/**
	 * Gets the size of the array (total number of elements) as an {@code int}.
	 * @return size of array
	 */
	public readonly get size() => ( int )
	{
		return cast<int>(sizeLong);
	}

	/**
	 * Copy a subarray from the given array, starting at {@code start} and going
	 * up to but not including index {@code end}.
	 * The resulting array will always be one-dimensional and requires an explicit
	 * cast in order to be stored back into a normal array reference.
	 * @param start	starting index of subarray
	 * @param end	index after last index in subarray
	 * @return {@code ArrayNullable<T>} object containing the subarray
	 */
	[ImportNative]
	public readonly subarray( long start, long end ) => ( ArrayNullable<T> );
	
	
	/**
	 * Gets element located at the specified index.
	 * @param index	location to read from, as a {@code long}	 
	 * @return value at given location
	 */	
	[ImportNative]
	public readonly index( long index ) => (nullable T);

	
	/**
	 * Sets element at the specified index to the given value.
	 * @param index	location to store at	 
	 * @param value value to be stored
	 */	
	[ImportNative]
	public index( long index, nullable T value ) => ();

	
	/**
	 * Gets formatted {@code String} representation of the array.   
	 * @return {@code String} representation
	 */
	public readonly toString() => ( String )
	{
		if ( this->sizeLong == 0L )
			return "[]";
		
		MutableString string = MutableString:create("[");
		boolean first = true;
		foreach( nullable T value in this )
		{	
			if ( first )
				first = false;
			else
				string.append(", ");
				
			string.append(value);
		}
		return string.append("]").toString();
	}
	
	/**
	 * Gets iterator that can iterate over all the elements of the array.   
	 * @return iterator
	 */
	public readonly iterator() => (IteratorNullable<T>)
	{
		return ArrayIterator<T>:create(this);
	} 
	
	private class ArrayIterator<T> is IteratorNullable<T>
	{
		long position = 0;
		immutable long limit;
		ArrayNullable<T> array;
		
		public create(ArrayNullable<T> array)
		{
			this:array = array;
			limit = array->sizeLong;
		}
		
		public readonly hasNext() => (boolean)
		{	
			return position < limit;		
		}
		
		public next() => (nullable T)
		{
			if( position >= limit )
				throw IndexOutOfBoundsException:create(position);
			
			nullable T value = array.index( position );			
			position += 1L;			
			
			return value;  
		}
	}
}
